-- @atlcompiler emftvm
-- @nsURI UML=http://www.eclipse.org/uml2/4.0.0/UML
-- @nsURI NOTATION=http://www.eclipse.org/gmf/runtime/1.0.2/notation


module uml2notation;
create OUT: NOTATION from IN: UML;


rule StateMashine2Diagram {
	from
		s: UML!StateMachine
	to
		t: NOTATION!Diagram (
			type <- 'PapyrusUMLStateMachineDiagram',
			name <- 'Diagram',
			element <:= s,
			children <- Sequence{shape},
			styles <- Sequence{style},
			edges <- UML!Transition.allInstances()
		),
		style: NOTATION!DiagramStyle (
		),
		shape: NOTATION!Shape (
			type <- '2000',
			fontName <- 'Segoe UI',
			lineColor <- 0,
			element <:= s,
			eAnnotations <- Sequence{thisModule.CreateEAnnotation('ShadowFigure',
					'ShadowFigure_Value', 'false'),
						thisModule.CreateEAnnotation('displayNameLabelIcon',
								'displayNameLabelIcon_value', 'false'),
						thisModule.CreateEAnnotation('QualifiedName',
								'QualifiedNameDepth', '1000')},
			children <- Sequence{n1,
						n2},
			layoutConstraint <- thisModule.CreateBounds(0, 0, 100, 100)
		),
		n1: NOTATION!DecorationNode (
			type <- '2001',
			layoutConstraint <- thisModule.CreateBounds(0, 0, 100, 100)
		),
		n2: NOTATION!DecorationNode (
			type <- '2002',
			layoutConstraint <- thisModule.CreateBounds(0, 0, 100, 100),
			children <- s.region
		)
}

rule Region2Shape {
	from
		s: UML!Region
	to
		t: NOTATION!Shape (
			type <- '3000',
			fontName <- 'Segoe UI',
			lineColor <- 0,
			element <:= s,
			eAnnotations <- Sequence{thisModule.CreateEAnnotation('ShadowFigure',
					'ShadowFigure_Value', 'false'),
						thisModule.CreateEAnnotation('displayNameLabelIcon',
								'displayNameLabelIcon_value', 'false'),
						thisModule.CreateEAnnotation('QualifiedName',
								'QualifiedNameDepth', '1000'),
						thisModule.CreateEAnnotation('RegionAnnotationKey',
								'RegionZoneKey', '')},
			children <- Sequence{n},
			layoutConstraint <- thisModule.CreateBounds(0, 0, 100, 100)
		),
		n: NOTATION!DecorationNode (
			type <- '3002',
			layoutConstraint <- thisModule.CreateBounds(0, 0, 0, 0),
			children <- s.subvertex
		)
}

rule State2Shape {
	from
		s: UML!State
	to
		t: NOTATION!Shape (
			type <- '6000',
			fontName <- 'Segoe UI',
			lineColor <- 0,
			element <:= s,
			eAnnotations <- Sequence{thisModule.CreateEAnnotation('ShadowFigure',
					'ShadowFigure_Value', 'false'),
						thisModule.CreateEAnnotation('displayNameLabelIcon',
								'displayNameLabelIcon_value', 'false'),
						thisModule.CreateEAnnotation('QualifiedName',
								'QualifiedNameDepth', '1000')},
			children <- Sequence{n1,
						n2},
			layoutConstraint <- thisModule.CreateBounds(50, 50, 80, 30)
		),
		n1: NOTATION!DecorationNode (
			type <- '6001',
			layoutConstraint <- thisModule.CreateBounds(0, 0, 20, 0 - 1)
		),
		n2: NOTATION!DecorationNode (
			type <- '6002',
			layoutConstraint <- thisModule.CreateBounds(0, 0, 20, 0 - 1),
			visible <- false
		)
}

rule Pseudostate2Shape {
	from
		s: UML!Pseudostate
	to
		t: NOTATION!Shape (
			type <- '8000',
			fontName <- 'Segoe UI',
			lineColor <- 0,
			element <:= s,
			eAnnotations <- Sequence{thisModule.CreateEAnnotation('ShadowFigure',
					'ShadowFigure_Value', 'false'),
						thisModule.CreateEAnnotation('displayNameLabelIcon',
								'displayNameLabelIcon_value', 'false'),
						thisModule.CreateEAnnotation('QualifiedName',
								'QualifiedNameDepth', '1000')},
			children <- Sequence{n1,
						n2},
			layoutConstraint <- thisModule.CreateBounds(50, 50, 10, 10)
		),
		n1: NOTATION!DecorationNode (
			type <- '8001',
			layoutConstraint <- thisModule.CreateLocation(10, 10)
		),
		n2: NOTATION!DecorationNode (
			type <- '8002',
			layoutConstraint <- thisModule.CreateLocation(10, 10)
		)
}

rule Transition2Connector {
	from
		s: UML!Transition
	to
		t: NOTATION!Connector (
			source <- s.source,
			target <- s.target,
			type <- '7000',
			lineColor <- 0,
			children <- Sequence{n1,
						n2,
						n3},
			styles <- Sequence{s1},
			element <:= s,
		
			sourceAnchor <- ia,
				bendpoints <- rbs
		),
		s1: NOTATION!FontStyle (
			fontName <- 'Segoe UI'
		),
		n1: NOTATION!DecorationNode (
			type <- '7001',
			layoutConstraint <- thisModule.CreateLocation(0, 0)
		),
		n2: NOTATION!DecorationNode (
			type <- '7002',
			layoutConstraint <- thisModule.CreateLocation(0, 0)
		),
		n3: NOTATION!DecorationNode (
			type <- '7003',
			layoutConstraint <- thisModule.CreateLocation(0, 0)
		),
		rbs: NOTATION!RelativeBendpoints (
			--- Sequence{'[7, 12, -47, -70]','[51, 114, -3, 32]'}
		),
		ia: NOTATION!IdentityAnchor (
			id <- '(0.6435643564356436,0.8285714285714286)'
		)
}

rule CreateEAnnotation(source: String,key: String, value: String) {
	to
		an: NOTATION!EAnnotation (
			source <- source,
			details <- Sequence{sm}
		),
		sm: NOTATION!EStringToStringMapEntry (
			key <- key,
			value <- value
		)
	do {
		an; 
	}
}

rule CreateBounds(newX: Integer, newY: Integer, newWidth: Integer, newHeight: Integer) {
	to
		b: NOTATION!Bounds (
			x <- newX,
			y <- newY,
			width <- newWidth,
			height <- newHeight
		)
	do {
		b; 
	}
}

rule CreateLocation(newX: Integer, newY: Integer) {
	to
		b: NOTATION!Location (
			x <- newX,
			y <- newY
		)
	do {
		b; 
	}
}
