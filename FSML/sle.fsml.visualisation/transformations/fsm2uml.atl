-- @atlcompiler emftvm
-- @nsURI FSML=http://www.fsml.sle/FSML
-- @nsURI UML=http://www.eclipse.org/uml2/4.0.0/UML


module fsm2uml;
create OUT: UML from IN: FSML;


entrypoint rule Model() {
	to
		t: UML!Model (
			name <- 'Model',
			packagedElement <- Sequence{sm}
		),
		sm: UML!StateMachine (
			name <- 'State Machine',
			region <- Sequence{region}
		),
		region: UML!Region (
			name <- 'Main Region',
			subvertex <- FSML!FSMState.allInstances(),
			transition <- FSML!FSMTransition.allInstances()
		)
}

rule State2State {
	from
		s: FSML!FSMState (
			not s.initial
		)
	to
		t: UML!State (
			name <- s.name
		)
}

rule State2Pseudostate {
	from
		s: FSML!FSMState (
			s.initial
		)
	to
		t: UML!Pseudostate (
			name <- s.name
		)
}

rule Transition2Transition {
	from
		s: FSML!FSMTransition
	to
		t: UML!Transition (
			source <- s.refImmediateComposite(),
			target <- s.target,
			name <- s.input.concat(if s.action = OclUndefined then
						''
					else
						'/' + s.action
					endif)
		)
}
