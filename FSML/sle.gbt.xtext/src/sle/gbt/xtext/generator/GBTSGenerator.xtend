/*
 * generated by Xtext
 */
package sle.gbt.xtext.generator

import org.eclipse.emf.ecore.resource.Resource
import org.eclipse.xtext.generator.IGenerator
import org.eclipse.xtext.generator.IFileSystemAccess

import sle.gbt.xtext.icc.ICC
import org.eclipse.emf.ecore.EObject
import java.util.List
import org.eclipse.xtext.ParserRule
import sle.gbt.xtext.icc.XtextToSG
import sle.gbt.xtext.gBTS.Apply
import sle.gbt.xtext.gBTS.Test
import sle.gbt.xtext.gBTS.Sub
import sle.gbt.xtext.gBTS.New
import sle.gbt.sg.SgFactory

/**
 * Generates code from your model files on save.
 * 
 * see http://www.eclipse.org/Xtext/documentation.html#TutorialCodeGeneration
 */
class GBTSGenerator implements IGenerator {
	extension SgFactory sgFactory = SgFactory.eINSTANCE
	
	override void doGenerate(Resource resource, IFileSystemAccess fsa) {
		for (test : resource.allContents.filter(Test).toIterable) {

			// Extract terminals and grammar
			val terminals = XtextToSG.terminalsFrom(test.ref)
			val grammar = XtextToSG.grammarFrom(test.ref)

			for (sub : test.members.filter(Sub)) {
				grammar.put(sub.rule.name, sub.substitution)
			}
			
			val newterms = newArrayList
			
			for (^new : test.members.filter(New)) {
				grammar.put(^new.rule, ^new.definition)
				
				if(^new.term)
					newterms.add(0, ^new.rule)
			}
			
			terminals += newterms

			val lbr = if(test.hasLbr)
					test.lbr
				else
					ICC.DEEPENING_LBR_DEFAULT

			val lbrInitial = if(test.hasLbrInitial)
					test.lbrInitial
				else
					ICC.INITIAL_LBR

			// Create iterator host
			val icc = new ICC(lbr, terminals, grammar)
			
			// Create the ranges
			val range = newArrayList
			for (apply : test.members.filter(Apply)) {
				val num = if(apply.hasNum)
						apply.num
					else
						10

				if(apply.hasMax)
					range += (0 ..< num).map[apply.minOrIt + it * (apply.max - apply.minOrIt) / num]
				else
					range += (0 ..< num).map[apply.minOrIt + it]
			}

			// Find the start-rule
			val startrule = if(test.hasStartrule)
					test.startrule.name
				else
					test.ref.rules.filter(ParserRule).head.name

			// Generate the testfile
			fsa.generateFile('''«test.name».htm''',
				'''
					<html>
						<head>
							<title>«test.name»</title>
						</head>
						<body>
							<p><table border="1" width="640">
								<tr>
									<th colspan="2">Generated tests</th>
								</tr>
							«FOR i : range SEPARATOR '''<tr>
									<td colspan="2">&nbsp;</td>
								</tr>'''»
								<tr>
									<td>«i»</td>
								</tr>
								<tr>
									<td><code>«icc.iterate(grammar.get(startrule), lbrInitial).iterator(i).head»</code></td>
								</tr>
							«ENDFOR»
							</table></p>
							
							<p><table border="1" width="640">
								<tr>
									<th colspan="2">Generated from the derived specification</th>
								</tr>
								<tr>
									<td>Initial LBR</td>
									<td>«lbrInitial»</td>
								</tr>
								<tr>
									<td>LBR</td>
									<td>«lbr»</td>
								</tr>
							<tr>
								<td colspan="2">&nbsp;</td>
							</tr>
							«FOR ti : terminals.size >.. 0»
								<tr>
									<td>Terminal #«ti»</td>
									<td>«terminals.get(ti)»</td>
								</tr>
							«ENDFOR»
							<tr>
								<td colspan="2">&nbsp;</td>
							</tr>
							«FOR ge : grammar.entrySet»
								<tr>
									<td>Rule «ge.key»</td>
									<td>«ge.value»</td>
								</tr>
							«ENDFOR»
							</table></p>
						</body>
					</html>	
				''')
		}
	}
}
